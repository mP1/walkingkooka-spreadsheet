(* general expressions *)
EXPRESSION 		                = [ WHITESPACE ], EXPRESSION_REQUIRED;
EXPRESSION_OPT                  = TEXT | BINARY_EXPRESSION | FUNCTION | RANGE | CELL | GROUP | NEGATIVE | PERCENTAGE | NUMBER;
EXPRESSION_REQUIRED             = EXPRESSION_OPT;


BINARY_EXPRESSION               = BINARY_SUB_EXPRESSION, {[ WHITESPACE ], BINARY_OPERATOR, [ WHITESPACE ], BINARY_SUB_EXPRESSION_REQUIRED};
BINARY_OPERATOR                 = EQUALS_SYMBOL | NOT_EQUALS_SYMBOL | GREATER_THAN_EQUALS_SYMBOL | GREATER_THAN_SYMBOL | LESS_THAN_EQUALS_SYMBOL | LESS_THAN_SYMBOL | PLUS_SYMBOL | MINUS_SYMBOL | MULTIPLY_SYMBOL | DIVIDE_SYMBOL | POWER_SYMBOL | BETWEEN_SYMBOL;
BINARY_SUB_EXPRESSION           = FUNCTION | CELL | GROUP | NEGATIVE | PERCENTAGE | NUMBER;
BINARY_SUB_EXPRESSION_REQUIRED  = BINARY_SUB_EXPRESSION;

(* cell column/row OR label *)
CELL			                = LABEL_NAME | COLUMN_ROW;
CELL_REQUIRED                   = CELL;

RANGE                           = CELL, [ WHITESPACE ], BETWEEN_SYMBOL, [ WHITESPACE ], CELL_REQUIRED;

(* function aka function( 123, 456 ) *)
FUNCTION                        = FUNCTION_NAME, PARENTHESIS_OPEN_SYMBOL, [ WHITESPACE ], [FUNCTION_PARAMETERS], [ WHITESPACE ], PARENTHESIS_CLOSE_SYMBOL;
FUNCTION_PARAMETERS             = EXPRESSION_OPT, [{FUNCTION_PARAMETER_OTHERS}];
FUNCTION_PARAMETER_OTHERS       = [ WHITESPACE ], FUNCTION_PARAMETER_SEPARATOR_SYMBOL, [ WHITESPACE ], EXPRESSION_REQUIRED;

GROUP 			                = PARENTHESIS_OPEN_SYMBOL, [ WHITESPACE ], EXPRESSION_REQUIRED, [ WHITESPACE ], PARENTHESIS_CLOSE_SYMBOL;

(* NEGATIVE_EXPRESSION ... NUMBER must not have BINARY_EXPRESSION to avoid incorrect tokens. *)
NEGATIVE_EXPRESSION_REQUIRED    = FUNCTION | CELL | GROUP | PERCENTAGE | NUMBER;
NEGATIVE		                = MINUS_SYMBOL, [ WHITESPACE ], NEGATIVE_EXPRESSION_REQUIRED;

PERCENTAGE		                = NUMBER, PERCENT_SYMBOL;
